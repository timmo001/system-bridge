!include "MUI2.nsh"
!include "TextFunc.nsh"
!include "StrFunc.nsh"
${StrStrAdv}

Name "System Bridge"
OutFile "dist\system-bridge-$VERSION-setup.exe"
InstallDir "$PROGRAMFILES64\System Bridge"

!define MUI_ICON ".resources\system-bridge-dimmed.ico"
!define MUI_UNICON ".resources\system-bridge-dimmed.ico"

VIProductVersion "$DOT_VERSION"
VIAddVersionKey "ProductName" "System Bridge"
VIAddVersionKey "FileDescription" "System Bridge - A bridge for your systems"
VIAddVersionKey "CompanyName" "System Bridge"
VIAddVersionKey "LegalCopyright" "Copyright (C) $YEAR Aidan Timson"
VIAddVersionKey "OriginalFilename" "system-bridge-setup.exe"
VIAddVersionKey "InternalName" "system-bridge-setup.exe"
VIAddVersionKey "ProductVersion" "$VERSION"
VIAddVersionKey "FileVersion" "$VERSION"

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

!insertmacro MUI_LANGUAGE "English"

Section "Prerequisites"
  # Check for Visual C++ Runtime
  IfFileExists "$SYSDIR\vcruntime140*.dll" vc_runtime_ok
    MessageBox MB_YESNO|MB_ICONQUESTION "This application requires Visual C++ Runtime. Would you like to install it now?" IDYES install_vc IDNO abort_vc
    abort_vc:
      Abort "Installation cancelled: Visual C++ Runtime is required."
    install_vc:
      ExecWait 'winget install Microsoft.VCRedist.2015+.x64' $8
      ${If} $8 != 0
        MessageBox MB_OK|MB_ICONSTOP "Failed to install Visual C++ Runtime. Please install it manually from https://aka.ms/vs/17/release/vc_redist.x64.exe"
        Abort "Installation failed: Could not install Visual C++ Runtime."
      ${EndIf}
    Goto vc_runtime_end
  vc_runtime_ok:
    ; Visual C++ Runtime found, continue
  vc_runtime_end:

  # Check for .NET 8.0 Runtime (required for NowPlaying helper)
  !ifdef INCLUDE_NOW_PLAYING
    # Check if .NET 8.0 runtime is installed by trying to query installed runtimes
    # First check if dotnet command exists and can list runtimes
    ClearErrors
    ExecWait 'cmd /c dotnet --list-runtimes 2>nul | findstr /C:"Microsoft.NETCore.App 8.0" >nul 2>&1' $0
    ${If} $0 != 0
      # .NET 8.0 runtime not found, prompt for installation
      MessageBox MB_YESNO|MB_ICONQUESTION "This application requires .NET 8.0 Runtime for the Now Playing feature. Would you like to install it now?" IDYES install_dotnet IDNO abort_dotnet
      abort_dotnet:
        MessageBox MB_OK|MB_ICONINFORMATION "Installation will continue, but the Now Playing feature may not work without .NET 8.0 Runtime. You can install it later from https://dotnet.microsoft.com/download/dotnet/8.0"
        Goto dotnet_end
      install_dotnet:
        ExecWait 'winget install Microsoft.DotNet.DesktopRuntime.8 --silent --accept-package-agreements --accept-source-agreements' $1
        ${If} $1 != 0
          MessageBox MB_OK|MB_ICONSTOP "Failed to install .NET 8.0 Runtime. Please install it manually from https://dotnet.microsoft.com/download/dotnet/8.0"
          MessageBox MB_OK|MB_ICONINFORMATION "Installation will continue, but the Now Playing feature may not work without .NET 8.0 Runtime."
        ${EndIf}
    ${EndIf}
    dotnet_end:
  !endif
SectionEnd

Section "Install"
  SetOutPath "$INSTDIR"
  File "dist\system-bridge.exe"
  File /oname=system-bridge.ico ".resources\system-bridge-dimmed.ico"
  
  ; Include NowPlaying helper if available at build time
  !ifdef INCLUDE_NOW_PLAYING
    SetOutPath "$INSTDIR\now-playing"
    File ".\\.scripts\\windows\\now-playing\\*.*"
  !endif

  # Register uninstaller in Windows registry
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\System Bridge" "DisplayName" "System Bridge"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\System Bridge" "UninstallString" "$INSTDIR\uninstall.exe"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\System Bridge" "DisplayIcon" "$INSTDIR\system-bridge.ico"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\System Bridge" "DisplayVersion" "$VERSION"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\System Bridge" "Publisher" "System Bridge"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\System Bridge" "InstallLocation" "$INSTDIR"

  WriteUninstaller "$INSTDIR\uninstall.exe"

  CreateDirectory "$SMPROGRAMS\System Bridge"
  CreateShortcut /NoWorkingDir "$SMPROGRAMS\System Bridge\System Bridge.lnk" "$INSTDIR\system-bridge.exe" "backend --open-web-client" "$INSTDIR\system-bridge.ico" 0 SW_SHOWMINIMIZED "" "System Bridge"
SectionEnd

Section "Uninstall"
  Delete "$INSTDIR\system-bridge.exe"
  Delete "$INSTDIR\system-bridge.ico"
  Delete "$INSTDIR\uninstall.exe"

  RMDir "$INSTDIR"

  Delete "$SMPROGRAMS\System Bridge\System Bridge.lnk"
  RMDir "$SMPROGRAMS\System Bridge"

  ; Remove uninstaller registry keys
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\System Bridge"
SectionEnd
